extends Node3D

const ImageItem: PackedScene = preload("res://scenes/items/ImageItem.tscn")
const TextItem: PackedScene = preload("res://scenes/items/TextItem.tscn")
const RichTextItem: PackedScene = preload("res://scenes/items/RichTextItem.tscn")

const MarbleMaterial: Material = preload("res://assets/textures/marble21.tres")
const WhiteMaterial: Material = preload("res://assets/textures/flat_white.tres")
const WoodMaterial: Material = preload("res://assets/textures/wood_2.tres")
const BlackMaterial: Material = preload("res://assets/textures/black.tres")

const ANIMATE_OFFSET_Y: float = 4.0
const CEILING_DROP_Y: float = 2.0
const LIGHT_ENERGY: float = 3.0
const ANIMATE_DURATION: float = 0.5

@onready var _item_node: Node3D = $Item
@onready var _item: Node3D
@onready var _ceiling: Node3D = $Ceiling
@onready var _light: SpotLight3D = get_node("Item/SpotLight3D")
@onready var _frame: Node3D = get_node("Item/Frame")
@onready var _animate_item_target: Vector3 = _item_node.position + Vector3(0, ANIMATE_OFFSET_Y, 0)
@onready var _animate_ceiling_target: Vector3 = _ceiling.position - Vector3(0, CEILING_DROP_Y, 0)

var _item_tween: Tween = null
var _light_tween: Tween = null
var _ceiling_tween: Tween = null

func _start_animate() -> void:
	var tween_time: float = 0.0
	var visibility_range: float = $Item/Plaque.visibility_range_end

	# Check if any player is close enough to see/hear the animation
	for player in get_tree().get_nodes_in_group("Player"):
		if position.distance_to(player.global_position) <= visibility_range:
			tween_time = ANIMATE_DURATION
			$SlideSound.play()
			break

	if _item_tween and _item_tween.is_valid():
		_item_tween.kill()
	if _light_tween and _light_tween.is_valid():
		_light_tween.kill()
	if _ceiling_tween and _ceiling_tween.is_valid():
		_ceiling_tween.kill()

	_item_tween = create_tween()
	_light_tween = create_tween()
	_ceiling_tween = create_tween()

	_item_tween.tween_property(
		_item_node,
		"position",
		_animate_item_target,
		tween_time
	)

	_ceiling_tween.tween_property(
		_ceiling,
		"position",
		_animate_ceiling_target,
		tween_time
	)

	if Platform.is_compatibility_renderer():
		_light_tween.kill()
		_light.visible = false
	else:
		_light_tween.tween_property(
			_light,
			"light_energy",
			LIGHT_ENERGY,
			tween_time
		)

	_item_tween.set_trans(Tween.TRANS_LINEAR)
	_item_tween.set_ease(Tween.EASE_IN_OUT)

	_light_tween.set_trans(Tween.TRANS_LINEAR)
	_light_tween.set_ease(Tween.EASE_IN_OUT)

	_ceiling_tween.set_trans(Tween.TRANS_LINEAR)
	_ceiling_tween.set_ease(Tween.EASE_IN_OUT)

func _on_image_item_loaded() -> void:
	var size: Vector2 = _item.get_image_size()
	if size.x > size.y:
		_frame.scale.y = size.y / float(size.x)
	else:
		_frame.scale.x = size.x / float(size.y)
	_frame.position = _item.position
	_frame.position.z = 0
	_start_animate()

func get_image_item() -> Node:
	if _item and "image_url" in _item:
		return _item
	return null


func get_image_title() -> String:
	if _item and "title" in _item:
		return _item.title
	return ""


func set_stolen(stolen: bool) -> void:
	if _item and _item.has_method("set_stolen"):
		_item.set_stolen(stolen)


func init(item_data: Dictionary) -> void:
	if item_data.has("material"):
		if item_data.material == "marble":
			$Item/Plaque.material_override = MarbleMaterial
		if item_data.material == "white":
			$Item/Plaque.material_override = WhiteMaterial
		elif item_data.material == "none":
			$Item/Plaque.visible = false
			_animate_item_target.z -= 0.05

	if item_data.type == "image":
		_item = ImageItem.instantiate()
		_item.loaded.connect(_on_image_item_loaded)
		var plate = item_data.get("plate", "")
		_item.init(item_data.get("title", ""), item_data.get("text", ""), plate if plate else "")
	elif item_data.type == "text":
		_frame.visible = false
		_item = TextItem.instantiate()
		_item.init(item_data.text)
		_start_animate()
	elif item_data.type == "rich_text":
		_frame.visible = false
		_item = RichTextItem.instantiate()
		_item.init(item_data.text)
		_start_animate()
	else:
		return
	_item.position = Vector3(0, 0, 0.1)
	_item_node.add_child(_item)
